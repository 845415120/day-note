## [1 - 基础](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s1)

### [a. 下载并安装 AutoHotkey](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s11)

在学习使用 AutoHotkey(AHK) 之前, 你需要下载它. 下载后, 你可能会需要安装它. 但这取决于你下载的版本. 对于本指南, 我们将使用安装版, 因为对于新手来说, 它最容易设置.

文字指导:

1. 访问 AutoHotkey 主页: [https://autohotkey.com/](https://autohotkey.com/)
2. 点击下载: [https://autohotkey.com/download/ahk-install.exe](https://autohotkey.com/download/ahk-install.exe)
3. 在安装 Autohotkey 过程中, 需要你选择安装 UNICODE 或者 ANSI 版本. 你很可能需要 UNICODE 版本, 简单点说, 如果你想要它支持非英文字符和数字, 那就选择 UNICODE 版. 版本选择之后, 一直点下一步直到你看到安装(Install) 按钮.
4. 装完了? 棒极了! 我们接着往下看.

### [b. 如何创建一个脚本](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s12)

Autohotkey 安装完成后, 你也许会想它能做些什么. AutoHotkey 不是魔法, 虽然我们都希望它是. 所以需要我们告诉它要去干什么. 而这个过程叫做 "写脚本".

文字指导:
1. 右键点桌面空白处.
2. 点击 "新建" 菜单.
3. 点击里面的 "AutoHotkey Script" 新建一个脚本.
4. 给脚本命名. 备注: 文件名必须带 .ahk 后缀, 例如 MyScript.ahk
5. 找到刚刚新建的脚本并右键点击它.
6. 点击 "Edit Script".
7. 一个新窗口被弹出, 也许是记事本. 如果是这样就成功了!

现在你已经创建了一个脚本, 我们需要加点内容到脚本中. 如果需要用到内置命令, 函数和变量, 请查看[第 5 节](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s5).
这是一个使用 [Send](https://wyagd001.github.io/zh-cn/docs/lib/Send.htm) 命令创建的一个包含热键的简单脚本, 当你按下热键后, 它会向窗口发送一段文字.
```ts
^j::
Send, My First Script
Return
```

稍后我们将进行更深入的研究. 在此之前, 我们先解释一下上面的代码:

==- 第一行: `^j::` 是热键. `^` 代表 Ctrl, `j` 是字母 J. 任何在 `::` 左边的字符表示您需要按下的热键.==
- 第二行: `[Send], My First Script` 表示如何**发送**按键. Send 是命令, 任何在逗号(,) 之后的内容将会被键入.
- 第三行: . Return 将会成为你最好的朋友. 从字面上看, 它将 停止 代码继续往下. 当你的脚本包含越来越多的东西时, 使用 Return 会避免很多问题.

8. 保存文件.
9. 双击桌面上的文件来运行它, 打开记事本或者其他可以输入文字的地方然后按下 Ctrl 和 J.
10. 太好了! 你的第一个脚本完成了. 给自己一些奖励, 然后返回阅读本教程的其余部分.

### [c. 不要合并命令](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s13)

当你在写代码的时候, 你可能有强烈的冲动想在同一行放下几个命令, 或者在一个命令中嵌套多个命令, 千万不要这样做. 在[第 5 节](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s5)我们将会告诉你为什么不要这么做, 同时, 我们还将告诉你应该怎么做.

 ## [2 - 快捷键 & 热字串](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s2)
 
什么是热键? 热键是一个发热的按键, 开个玩笑. 热键是用来触发某些动作的按键或组合按键. 例如:

```ts
^j::
   Send, My First Script
Return
```
什么是热字串? 当你键入它们时, 热字串主要用于扩展缩写(自动替换). 当然, 它也可以用来启动任何脚本动作. 例如:

```
::ftw::Free the whales
```

这两个例子的区别在于, 当你按下 Ctrl+J 时, 热键将会触发, 而热字串会将你输入的 "ftw" 转换为 "Free the whales".

_"那么, 该如何创建一个热键?"_ 好问题. 热键是通过一对 :: 创建的. 按键名或组合按键名必须在 `::` 左边. 代码则跟在下面, 然后以  [Return]  结束.

如上所述, 热字串也可以启动脚本动作. 和热键一样能干好多事, 真棒!

```ts
::btw::
    MsgBox You typed "btw"
 Return   
```

### [a. 键和其神秘符号](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s21)

|符号|描述|
|---|---|
|#|Win(Windows 徽标键)|
|!|Alt|
|^|Ctrl|
|+|Shift|
|&|用于连接两个按键(含鼠标按键) 合并成一个自定义热键.|

你可以通过在两个按键(除了控制器按键) 之间, 使用  `&`  来定义一个组合热键. 在下面的例子中, 你要按下 Numpad0, 再按下 Numpad1 或 Numpad2, 才能触发热键:

```ts
Numpad0 & Numpad1::
MsgBox You pressed Numpad1 while holding down Numpad0.
Return

Numpad0 & Numpad2::
Run Notepad
Return
```

### [b. 窗口特定的热键/热字串](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s22)

有时候你也许想要热键或热字串只在某些特定窗口上工作(或禁用). 要做到这一点, 您需要使用其中的任意一个 "高级" 命令, 在它们前面带有一个 #:

```ts
#IfWinActive
#IfWinExist
```

这些特殊的命令(技术上称为 "指令") 可以创建对上下文敏感的热键和热字串. 只需指定一个窗口标题. 但在某些情况下, 你可能需要指定一个窗口句柄, 组或类. 如果想深入了解这些高级内容, 点这里: [WinTitle 参数 & 最近找到的窗口](https://wyagd001.github.io/zh-cn/docs/misc/WinTitle.htm).

```ts
#IfWinActive Untitled - Notepad  ; 无标题 - 记事本
#space::
MsgBox You pressed Win+Spacebar in Notepad.
Return
```

要关闭后续热键或热字串的上下文敏感性, 请指定任意 #IfWin 指令, 但将其所有参数留空. 例如:

```ts
; 无标题 - 记事本
#IfWinActive Untitled - Notepad
!q::
MsgBox, You pressed Alt+Q in Notepad.
Return

; 任何不是无标题 - 记事本的窗口
#IfWinActive
!q::
MsgBox, You pressed Alt+Q in any window.
Return
```

当 #ifwin 指令在脚本中从未使用, 所有的热键和热字串对所有窗口生效.

#IfWin 指令是与位置相关的: 它们将影响脚本中的位于他们之间所有热键和热字串. 它们也相互排斥的; 也就是说, 只有最近的一个会有效.

```ts
; 记事本
#IfWinActive ahk_class Notepad
#space::
MsgBox, You pressed Win+Spacebar in Notepad.
Return
::msg::You typed msg in Notepad

; 画图
#IfWinActive Untitled - Paint  ; 无标题 - 画图
#space::
MsgBox, You pressed Win+Spacebar in MSPaint!
Return
::msg::You typed msg in MSPaint!
```

### [c. 一个文件包含多个热键/热字串](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s23)

这是一些人的想法. 因此, 我在这里声明一下: AutoHotkey 有能力将 任意多 的热键和热字串放在一个文件中. 不管是 1 个, 还是 3253 个(或者更多).

```ts
#i::
Run, https://www.google.com/
Return

^p::
Run, notepad.exe
Return

~j::
Send, ack
Return

:*:acheiv::achiev
::achievment::achievement
::acquaintence::acquaintance
:*:adquir::acquir
::aquisition::acquisition
:*:agravat::aggravat
:*:allign::align
::ameria::America
```

上面的代码是完全可以接受的. 多个热键, 多个热字串, 都包含在一个大的脚本文件里.

### [d. 示例](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s24)

```ts
^n::  ; Ctrl+N 热键.
Run, notepad.exe  ; 当你按下 Ctrl+N, 将启动记事本.
Return   ; 热键内容结束. 当按下热键时, 下面的代码不会被执行.
```

```ts
^b::  ; Ctrl+B 热键
Send, {ctrl down}c{ctrl up}  ; 复制选定的文本. 也可以使用 ^c, 但这种方法更加可靠.
SendInput, [b]{ctrl down}v{ctrl up}[/b] ; 粘贴所复制的文本, 并在文本前后加上加粗标签.
Return  ; 热键内容结束, 这之后的内容将不会触发.

```

```ts
::btw::By the way  ; 替换 "btw" 为 "By the way", 当你按下默认的终止符的时候.

```

```ts
:*:btw::By the way  ; 替换 "btw" 为 "By the way" 而不需要按下终止符.

```

## [3 - 发送按键](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s3)

现在你决定发送(输入) 一些按键到一个程序中. 你可以使用 [Send](https://wyagd001.github.io/zh-cn/docs/lib/Send.htm) 命令. 该命令表示发送按键, 模拟打字或按键操作.

但是在我们准备使用 Send 之前, 还有一些常见问题要注意.

就像热键一样, Send 命令也有一些特殊的键. 这里列出 4 个最常见的特殊按键:

|符号|描述|
|---|---|
|!|发送 Alt. 例如, `Send This is text!a` 将发送按键序列 "This is text" 并接着按下 Alt+A. **注意:** `!A` 在某些程序中产生的效果与 `!a` 不同. 这是因为 `!A` 表示按下 Alt+Shift+A 而 `!a` 表示按下 Alt+A. 如果不确定, 请使用小写字母.|
|+|发送 Shift. 例如, `Send +abC` 会发送文本 "AbC", 而 `Send !+a` 会按下 Alt+Shift+A.|
|^|发送 Ctrl(Ctrl) 键. 例如, `Send ^!a` 会按下 Ctrl+Alt+A, 而 `Send ^{Home}` 则发送 Ctrl+Home. **注意:** `^A` 在某些程序中产生的效果与 `^a` 不同. 这是因为 `^A` 表示按下 Ctrl+Shift+A 而 `^a` 表示按下 Ctrl+A. 如果不确定, 请使用小写字母.|
|#|发送 Win (带有 Windows logo 的按键), 因此 `Send #e` 会在按住 Win 时按下字母 "e".|

实际上 **花括号**是需要的. 它将告诉 AutoHotkey `{!}` 表示 "感叹号", 而不是要 "按下 Alt".

```
Send, This text has been typed{!}
; 注意 {} 中的 !(感叹号)? 这是因为, 如果没有 {}, AHK 将按下 Alt 键.
```

### [a. 游戏](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s31)

**非常重要:** 一些游戏, 尤其是多人游戏, 使用反作弊程序, 例如 GameGuard, Hackshield, PunkBuster 等. 且不说绕开反作弊系统是违反游戏规定的, 绕开反作弊本身也不太容易实现.

如果游戏的反作弊系统导致你的热键, 热字串和 Send 命令失效, 你是不走运的. 然而有一些方法也许能提高在某些游戏中使用热键的可能性, 但_没人能打包票一定能行_. 所以, 尽可能尝试 所有 你能想到的办法, 不要轻易放弃.

还有一个关于 DirectX 的问题要注意. 当你在 DirectX 游戏中使用 AutoHotkey 碰到问题时, 试试 [FAQ](https://wyagd001.github.io/zh-cn/docs/FAQ.htm#games) 页面中描述的情况. 当你使用 PixelSearch, PixelGetColor 或 ImageSearch 命令时, 你可能会碰到更多关于 DirectX 的问题. 画面颜色可能会变成黑色(0x000000), 不管你设置的是什么颜色. 如果可能的话, 试试用窗口模式运行游戏. 这样做能够解决一些 DirectX 问题.

没有万能的办法能确保 AutoHotkey 能运行在所有程序里. 如果你试了所有的办法还是不行, 也许 AutoHotkey 暂时无法满足你的需要.

## [4 - 打开程序 & 网页](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s4)

想要打开诸如_画图(mspaint.exe), 计算器(calc.exe), 脚本.ahk_ 或一个文件夹, 你可以使用 [Run](https://wyagd001.github.io/zh-cn/docs/lib/Run.htm) 命令. 你还可以用这个命令打开一个网址, 比如 [https://autohotkey.com/](https://autohotkey.com/). 如果你想打开一个已经安装好的程序, 也很简单, 就像这样:

```
; 运行一个程序. 注意: 大部分的程序可能需要完整路径.
Run, C:\Program Files (x86)\Microsoft\Edge\Application\msedge.exe

; 打开一个网址
Run, https://autohotkey.com

; 使用 AHK 内置变量来打开 "我的文档"
Run, %A_MyDocuments%
```

## [5 - 命令和函数](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s5)

AutoHotkey 有两个重要的工具供开发者使用: 命令和函数.

### [命令](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#Commands)

你可以通过语法来分辨命令和函数. 命令后面的参数无需使用圆括号, 而函数需要使用. 所以命令就像下面这样:

```
Command, 参数1, 参数2, 参数3

```

当你使用命令时, 不能将几条命令放在同一行([IfEqual](https://wyagd001.github.io/zh-cn/docs/lib/IfEqual.htm "过时的. 新脚本应使用 If (表达式) 代替.") 除外). 也不能将一个命令作为参数插入到另一个命令. 例如:

```
MsgBox, Hello Run, Notepad.exe   ; 错误
MsgBox, Hello, Run, Notepad.exe  ; 错误

MsgBox, Hello      ; 正确
Run, Notepad.exe
```

跟函数不同的是, 命令使用 "传统语法". 这意味着 需要 在变量的前后加上 %, 比如 `%Var%`. 而文本和数字则不需要加双引号, 例如 `This is some text`. 另外, 与函数不同, 命令的参数不能进行运算.

### [函数](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#Functions)

如上所述, 函数不同于命令的地方在于函数需要使用圆括号. 一个典型的函数是这样的:

```
Function(参数1, 参数2, 参数3)
```
与命令相比, 函数有下面几个主要差异:
1. 你可以使用运算.
    
```
    SubStr(37 * 12, 1, 2)  
    SubStr(A_Hour - 12, 2)
```
    
2. 变量不需要加上百分号:
    
```
    SubStr(A_Now, 7, 2)
```
    
3. 函数可以嵌套另一个函数:
    
```
    SubStr(A_AhkPath, InStr(A_AhkPath, "AutoHotkey"))
```
    
4. 文本前后需要加上双引号:
    
```
    SubStr("I'm scripting, awesome!", 16)
```

函数通常会返回一个值, 这一点与命令也不一样. 命令需要设置 _OutputVar_ 参数来保存结果, 而函数不需要. 

将函数值赋给变量的最常用方法, 只需要这样做:

```
MyVar := SubStr("I'm scripting, awesome!", 16)
```

### [a. 代码块](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s51)

下面的代码中, 当 MyVar 等于 5 时, 所有行都会执行.

```
if (MyVar=5)
{
   MsgBox, MyVar equals %MyVar%!!
   ExitApp
}
```

## [6 - 变量](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s6)

[变量](https://wyagd001.github.io/zh-cn/docs/Variables.htm)就像一个包含信息的便利贴. 它可以用于函数, 命令或数学表达式中, 充当存储文本, 数字, 数据的作用. 如果没有变量, 程序和脚本将会非常乏味.

给变量赋值有很多方法, 我们将会讨论最常见的几种方法. 请特别留意等号(`=`).

传统的文本赋值
```
MyVar = Text
```
这是给变量赋值最简单的方法, 也是传统的赋值方法. 只需要在等号后面输入文本就行了.

传统的变量赋值
```
MyVar = %MyVar2%
```
和上面的方法类似, 只是你将一个变量所对应的值赋给了另一个变量.

传统的混合赋值
```
MyVar = %MyVar2% some text %MyVar3%.
```
以上两个传统赋值的组合.

表达式文本赋值
```
MyVar := "Text"
```
这是一个表达式赋值, 因为 : 在 = 之前. 任何文本都需要在引号中间.

表达式变量赋值
```
MyVar := MyVar2
```
在表达式模式中, 变量不需要百分号.

表达式数字赋值
```
MyVar := 6 + 8 / 3 * 2 - Sqrt(9)
```
感谢表达式, 你能进行计算!

表达式混合赋值
```
MyVar := "The value of 5 + " MyVar2 " is: " 5 + MyVar2
```
以上三个表达式赋值的组合.

### [a. 什么时候使用百分号](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s61)
关于变量一个最常见的问题是什么时候使用百分号(**%**). 希望下面这些内容能够消除一些困惑.

什么时候要使用百分号:

- 当你使用命令时(见前文), 参数是输出变量 _OutputVar_ 或输入变量 _InputVar_ 时除外.
- 当你使用传统模式(等号前面没有其他符号) 给一个变量赋值时.

什么时候 不要 使用百分号:

参数中的输入或输出变量. 例如: StringLen, OutputVar, InputVar
赋值时左边的变量: 例如: Var = 123abc
在传统 If 语句(不包括表达式) 中, 中左边的变量: If Var1 < %Var2%
在表达式中的变量, 例如:


### [b. 获取用户输入](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s62)

有时候你想让用户来选择某些值. 这可以有很多种方法, 但其中最简单的办法就是使用 [InputBox](https://wyagd001.github.io/zh-cn/docs/lib/InputBox.htm) 命令. 下面的例子展示了如何向用户提出一堆问题并根据用户的输入完成一些事情:

```ts
InputBox, OutputVar, Question 1, What is your first name?
if (OutputVar="Bill")
   MsgBox, That's an awesome name`, %OutputVar%.

InputBox, OutputVar2, Question 2, Do you like AutoHotkey?
if (OutputVar2 = "yes")
   MsgBox, Thank you for answering %OutputVar2%`, %OutputVar%! We will become great friends.
 else
   MsgBox, %OutputVar%`, That makes me sad.
```

## [7 - 对象](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s7)

### [a. 创建对象](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s71)

方括号语法
```
MyObject := ["one", "two", "three", 17]
```


大括号语法
```
Banana := {"Color": "Yellow", "Taste": "Delicious", "Price": 3}
```

数组函数
```
MyObject := Array("one", "two", "three", 17)
```
这种方式跟方括号语法形式一样, 区别仅仅是采用了函数的形式.

对象函数
```
Banana := Object("Color", "Yellow", "Taste", "Delicious", "Price", 3)
```
这种方式跟大括号语法形式一样, 区别仅仅是采用了函数的形式.

请注意, 所有这些方法都创建了同一样东西(也就是对象), 区别仅仅是对象的键不一样.

### [b. 使用对象](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s72)

使用对象有很多方式, 包括检索值, 设置值, 添加更多的值等等.
设置值
方括号表示法
```
Banana["Pickled"] := True ; 这个香蕉烂透了. 呃...
```
在对象中设置值跟设置变量的值一样简单. 你需要做的是把方括号所代表的值在表达式赋值运算符 := 的左边.

句点表示法
```
Banana.Consistency := "Mushy"
```
和上面一样, 但是用点(.) 符号.

检索值
方括号表示法
```
Value := Banana["Color"]
```
这允许你使用表达式作为键从对象中获取值. 在这个例子中, 我使用表达式 "Color", 不要惊讶我仅使用键 Color 就会得到一个包含单词 "Yellow" 的值, 因为我们事先(在上一节) 就设置了键 Color.

句点表示法
```
Value := Banana.Color
```
仅允许使用原样的字串作为键名. 在句点表示法中, 键名不能使用变量.

增加新的键和值
方括号表示法
```
MyObject["NewerKey"] := 3.1415
```
想要直接添加一对键和值, 只需设置一个尚不存在的键即可.

句点表示法
```
MyObject.NewKey := "Shiny"
```
和上面一样, 但是用点符号.

InsertAt(在..插入) 法
```
MyObject.InsertAt(Index, Value1, Value2, Value3...)
```
Index(索引) 为任意整数键. 这会将 所有 更高的整数键的索引向上移动插入值的数量, 即使是空缺的也一样(例如, 假设只有键 1 和 100 存在, 当插入一个值到第 50 个键的位置时, 将导致原来位于 100 的键的索引变成 101).

Push(推送) 法
```
MyObject.Push(Value1, Value2, Value3...)
```
"追加" 值到数组 MyObject 的尾部. 换句话说, 它将插入的值放在 最高整数键 + 1 的位置.

移除键和值:
用空白填充值
```
Banana.Consistency := ""
```
最简单的删除值的方法就是用空白填充. 你可以将其赋值为 ""(两个连续的双引号), 也就是常说的 空字符串. 这不会删除键, 但是它将使值看起来与从未赋值一样. 可以通过使用 HasKey 方法得知键依然存在, 而且键也会出现在 For 循环中. (我们一会再解释 For 循环)

删除法
```
RemovedValue := MyObject.Delete(AnyKey)
```
这和接下来的方法将删除键 和 值. MyObject[AnyKey] 先前的值将存储在 RemovedValue 中.

```
NumberOfRemovedKeys := MyObject.Delete(FirstKey, LastKey)
```
允许你删除 FirstKey 和 LastKey 之间的一系列的编号/整数或字符串键. 它给出的值将是被删除的键的数量, 如果你的键之间有间隙, 这是有用的(例如你指定键从 1 到 4, 但是键 2 不存在, 这将设置 NumberOfRemovedKeys 为 3, 因为只有三个键被移除).

Pop(抛出) 法
```
MyObject.Pop()
```
这将删除最高的整数键, 并返回这个键对应的值. 而且这种形式不会影响其他键的索引顺序(因为没有比它更高的序列了).

RemoveAt(在..删除) 方法
```
RemovedValue := MyObject.RemoveAt(Index)
	
NumberOfRemovedKeys := MyObject.RemoveAt(Index, Length)
```
这将移除从 Index 到 Index + Length - 1(包含) 之间的所有键(例如 Index 为 5, Length 为 3, 则将删除键 5, 6, 7 这三个键). 如果 Length 省略则默认为 1. 移除这些键之后, 更高数字或整数的键将向下填充空缺, 所以如果有个值位于 Index + Length 那么它现在的位置就是 Index. 这很像 InsertAt 方法操作多个指定值的操作.

## [8 - 其他有用的东西](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s8)

### [a. 神秘的 [ ]](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s81)

方括号中的内容代表 可选的
### [b. 查找你的 AHK 版本](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s82)
`MsgBox, %A_AhkVersion%`

### [d. 缩进](https://wyagd001.github.io/zh-cn/docs/Tutorial.htm#s84)


